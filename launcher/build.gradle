apply plugin: "java-library"
apply plugin: "com.github.johnrengelman.shadow"

dependencies {
  // API
  implementation(project(":api"))

  // Access Transformers
  implementation("net.minecraftforge:accesstransformers:2.2.1:service") {
    exclude group: "org.apache.logging.log4j"
  }

  // Core
  compile("cpw.mods:modlauncher:8.0.9") {
    exclude group: "org.apache.logging.log4j"
  }

  compile("cpw.mods:grossjava9hacks:1.3.0") {
    exclude group: "org.apache.logging.log4j"
  }
}

jar {
  manifest {
    attributes "Main-Class": "com.mineteria.ignite.IgniteBootstrap"
    attributes "Premain-Class": "com.mineteria.ignite.agent.Agent"

    attributes([
      "Specification-Title": "Ignite",
      "Specification-Vendor": "Mineteria",
      "Specification-Version": 1.0,
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "Mineteria"
    ], "com/mineteria/ignite/")

    from { zipTree { configurations.compile.files.find { entry -> entry.name.contains("modlauncher") } }.matching { include "**/MANIFEST.MF" }.singleFile }
  }
}

shadowJar {
  classifier "shaded"

  mergeServiceFiles()
  transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)

  dependencies {
    // API
    include dependency("org.apache.logging.log4j:log4j-api")
    include dependency("org.apache.logging.log4j:log4j-core")
    include dependency("org.checkerframework:checker-qual")
    include dependency("net.minecrell:terminalconsoleappender")
    include dependency("org.jline:jline-reader")
    include dependency("org.jline:jline-terminal")
    include dependency("org.jline:jline-terminal-jna")
    include dependency("net.java.dev.jna:jna")

    // Common
    include dependency("com.google.guava:guava")
    include dependency("com.google.code.gson:gson")

    // ASM
    include dependency("org.ow2.asm:asm")
    include dependency("org.ow2.asm:asm-analysis")
    include dependency("org.ow2.asm:asm-commons")
    include dependency("org.ow2.asm:asm-tree")
    include dependency("org.ow2.asm:asm-util")

    // Mixin
    include dependency("org.spongepowered:mixin")

    // Access Transformers
    include dependency("net.minecraftforge:accesstransformers")
    include dependency("org.antlr:antlr4-runtime")

    // Core
    include dependency("cpw.mods:modlauncher")
    include dependency("cpw.mods:grossjava9hacks")
    include dependency("net.sf.jopt-simple:jopt-simple")
  }

  relocate "com.google", "com.mineteria.ignite.relocate.google"
}

artifacts {
  archives shadowJar
}
